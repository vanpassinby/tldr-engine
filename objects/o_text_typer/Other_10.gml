/// @description execute command
var arg = []

var __temp_arg = ""
var __string_mode = false
for (var i = 1; i <= string_length(argstrings); i ++) {
    var __char = string_char_at(argstrings, i)
    
    if __char == "`"
        __string_mode = !__string_mode
    else if __char == "," && !__string_mode {
        __temp_arg = string_trim(__temp_arg)
        array_push(arg, __temp_arg)
        
        __temp_arg = ""
    }
    else
        __temp_arg += __char
}

// add the last recorded argument as well
if __temp_arg != "" {
    __temp_arg = string_trim(__temp_arg)
    array_push(arg, __temp_arg)
}

#region Basic typer commands

	/*	{sleep(frames)} : Pauses typing for a set amount of time.
		 - frames (Real): The amount of time to pause for.
	*/
	if command == "s" || command == "sleep" { // sleep(frames)
		if array_length(arg) > 0
			pause = real(arg[0])
		else
			show_error("Command sleep recieved no arguments",true)
	
		looping = false
	}
	
	// {pause()} OR {pause} : Pauses typing until [CONFIRM] is pressed.
	if command == "p" || command == "pause" {
		pause = -1
		looping = false
	    superskipping_buffer = 2
	}
	
	// {clear()} OR {clear} : Clears all previously written text. 
	if command == "c" || command == "clear" {
		event_user(2)
	}
	
	// {break()} OR {break} : Performs a manual line break.
	if command == "br" || command == "break" {
		chars ++
	}

	// {end()} OR {end} : Ends dialogue. This command destroys the typer automatically.
	if command == "e" || command == "end" { // end() OR end
		event_user(3)
	}
	
	// {stop()} OR {stop} : Stops the typer. This command does not automatically end the dialogue box, so you will have to deal with that manually.
	if command == "stop" || command == "st" { // stop() or stop
		pause = -2
    
	    looping = false
	    allow_skip_internal = false
	    skipping = false
	    superskipping = false
	}

#endregion

#region Text formatting

	/*	{col(val)} : Sets the color for text that's going to be typed.
		 - val (String): The color to change text to.  
	*/
	if command == "col" || command == "color" || command == "colour" { // col(string) OR color(string)
		saved_color = xcolor
		xcolor = string_to_color(arg[0])
		if arg[0] == "tired_aqua" 
			xcolor = merge_color(c_aqua, c_blue, 0.3)
	}
	if command == "s_col" || command == "solid_col" || command = "solid_color" || command == "solid_colour" { // solid_col(bool)
	    solid_color = string_to_bool(arg[0])
	}
	if command == "r_col" || command == "reset_col" || command == "reset_color" || command == "reset_colour" { // reset_col() OR reset_col
		xcolor = saved_color
	}
	if command == "font" || command == "fnt" { // font(`string`) out of the localized fonts or a reference to an asset by its name
		if loc_exists($"font_{arg[0]}")
			font = loc_font(arg[0])
		else
			font = asset_get_index(arg[0])
	}
	if command == "shadow" || command == "sh" { // shadow(bool)
		shadow = string_to_bool(arg[0])
	}

#endregion



	
	
if command == "auto_pauses" || command == "ap" { // auto_pauses(bool)
	var __a = string(arg[0])
	if __a == "true" 
		|| __a == "false"
		|| __a == "0"
		|| __a == "1"
	{
		auto_pauses = string_to_bool(__a)
	}
	else
		show_error("Command auto_pauses recieved a non-boolean argument", true)
}
if command == "auto_breaks" || command == "abr" { // auto_breaks(bool)
	var __a = string(arg[0])
	if __a == "true" 
		|| __a == "false"
		|| __a == "0"
		|| __a == "1"
	{
		auto_breaks = string_to_bool(__a)
	}
	else
		show_error("Command auto_breaks recieved a non-boolean argument", true)
}
if command == "break_system" || command == "br_sys" { // break_system(`language_id`)
	break_system = arg[0]
}

if command == "instant" || command == "inst" || command == "i" { // instant(bool = true)
    var __arg = true
    if array_length(arg) > 0
        __arg = arg[0]
    
	skipping = __arg
}
if command == "break_tabulation" || command == "br_tab" { // break_tabulation(bool)
	break_tabulation = string_to_bool(arg[0])
}
if command == "preset" || command == "preset" { // preset(`type`) out of `enemy_text`, `god_text`, `light_world`
	if arg[0] == "enemy_text" {
		break_tabulation = false
		font = loc_font("enc")
		shadow = false
		xscale = 1
		yscale = 1
		xcolor = c_black
		yspace = 20
	}
	if arg[0] == "god_text" {
		break_tabulation = false
		shadow = false
		god = true
		
		xspace = 5
		yspace = 20
		
		typespd = 2
		voice = -1
	}
    if arg[0] == "light_world" {
		shadow = false
	}
}
if command == "box_pos" || command == "bpos" { // box_pos(bool)
    caller._reposition_self_to(arg[0])
    
    if instance_exists(face_inst)
        face_inst.y = y
}

	

/// available effects:
/// shake (power = 1.0)
/// light_shake  --  will shake rarely as it's being rounded to the nearest round number. power equals to 0.51
/// wave (power = 1.0, frequency = 4.0)

if command == "fx" || command == "eff" || command == "effect" { // eff(string, [effect_arguments])  assigns an effect to all symbols after the command is called until eff(reset) is called. all arguments are given as string type
    effect = string_lower(arg[0])
    
    // support older versions
    if arg[0] == "-1"
        effect = undefined
    else if arg[0] == "1"
        effect = "wave"
    else if arg[0] == "2"
        effect = "shake"
    
    if array_length(arg) > 1 {
        effect_arguments = []
        array_copy(effect_arguments, 0, arg, 1, array_length(arg) - 1)
    }
}
if command == "fx_r" || command == "eff_r" || command == "eff_reset" || command == "effect_reset" { // eff_reset(reset_arguments = true)  resets the current effect. if the argument is false, the effect_arguments will not be reset. true by default, though
    effect = undefined
    
    if array_length(arg) > 0 && !string_to_bool(arg[0])
        effect_arguments = []
}

if command == "god" || command == "g" { // god(bool)  whether it's god (gaster) text
	god = string_to_bool(arg[0])
}

#region NPC linking commands

	if command == "l" || command == "link" || command == "npc_link" { // link(real, unlink_previous=bool, object=o_ow_npc)  you can link an npc to this and they will be animated when the text is playing (argument is npc id, second argument is true by default)
		var o_link = real(arg[0])
		var o = noone
	    var target = (array_length(arg) > 2 ? asset_get_index(arg[2]) : o_ow_npc)
    
		with (target) {
	        if !variable_instance_exists(self, "link_id")
	            continue
			if o_link == link_id
				o = id
		}
    
		if array_length(arg) > 1 && arg[1] // unlink previous talk links
	        talk_link = []
		if !array_contains(talk_link, o)
	        array_push(talk_link, o)
	}
	if command == "ul" || command == "unlink" || command == "npc_unlink" { // unlink(real)  you can link an npc to this and they will be animated when the text is playing (argument is npc id)
	    var o_link = real(arg[0])
		var o = noone
		var target = (array_length(arg) > 2 ? asset_get_index(arg[2]) : o_ow_npc)
    
		with (target) {
	        if !variable_instance_exists(self, "link_id")
	            continue
			if o_link == link_id
				o = id
		}
	
		if array_contains(talk_link, o)
	        array_delete(talk_link, array_get_index(talk_link, o), 1)
	}

	if command == "link_var_set" || command == "l_var" { // link_var_set(variable_name, value, is_real = false)
	    for (var i = 0; i < array_length(talk_link); i ++) {
	        if instance_exists(talk_link[i]) {
	            var val = arg[1]
	            if array_length(arg) > 2 && arg[2]
	                val = real(val)
            
	            variable_instance_set(talk_link[i], arg[0], val)
	        }
            
	    }
	}
	if command == "link_sprite_set" || command == "l_spr" { // link_sprite_set(sprite_name)
	    for (var i = 0; i < array_length(talk_link); i ++) {
	        var asset = asset_get_index(arg[0])
	        if instance_exists(talk_link[i]) && asset != -1
	            variable_instance_set(talk_link[i], "sprite_index", asset)
	    }
	}
	
#endregion


if command == "chc" || command == "choice" { // choice(`choice1`, `choice2`, ...)  create a choice box for the player
    _facechange("none");
    text_typer_choicer(arg, id);
    
	pause = -2
	looping = false
    
    choice_save_allow_skip = allow_skip_internal
    
    skipping = false
    superskipping = false
    allow_skip_internal = false
}

if command == "xscale" || command == "xsc" { // xscale(real)
	xscale = real(arg[0])
}
if command == "yscale" || command == "ysc" { // yscale(real)
	yscale = real(arg[0])
}
if command == "scale" || command == "sc" { // scale(real)
	xscale = real(arg[0])
	yscale = real(arg[0])
}
if command == "xspace" || command == "xsp" { // xspace(real)
	xspace = real(arg[0])
}
if command == "yspace" || command == "ysp" { // yspace(real)
	yspace = real(arg[0])
}
if command == "resetx" || command == "rx" { // resetx() OR resetx  reset the x position of the typer
	xoff = 0
	chars ++
}

if command == "spr" || command == "sprite" { // spr(sprite_index, image_speed=0, image_index=0) OR sprite(sprite_index, image_speed=0, image_index=0)  inserts a sprite into the text
	var spr = asset_get_index(arg[0])
	var inst = instance_create(o_text_single, x + xoff, y + yoff, depth)
	
	array_push(mychars, inst)
	
	inst.symbol = ""
	inst.font = font
	inst.gui = gui
	inst.scalex = xscale
	inst.scaley = yscale
	inst.xcolor = xcolor
	inst.font = font
	inst.shadow = shadow
	inst.effect = effect
	inst.timer = chartimeroff * chars
	inst.god = god
	inst.sprite = spr
    
    if array_length(arg) > 1
        inst.img_spd = arg[1]
    if array_length(arg) > 2
        inst.img_index = arg[2]
     
	xoff += sprite_get_width(spr)*xscale
	xoff += xspace * xscale
}
if command == "sound" || command == "snd" { // snd(sound_index) OR sound(sound_index)  plays a sound 
    var snd = asset_get_index(arg[0])
    
    if audio_exists(snd)
        audio_play(snd)
}

if command == "can_skip" || command == "skip" || command == "sk" { // can_skip(bool)
	can_skip = string_to_bool(arg[0])
    if !can_skip {
        allow_skip_internal = false
        skipping = false
        superskipping = false
    }
    else {
        allow_skip_internal = true
    }
}
if command == "can_superskip" || command == "superskip" || command == "ssk" { // can_superskip(bool)
	can_superskip = string_to_bool(arg[0])
    if !can_superskip {
        allow_skip_internal = false
        skipping = false
        superskipping = false
    }
    else {
        allow_skip_internal = true
    }
}

if command == "speed" || command == "spd" { // speed(real)
	typespd = real(arg[0])
}

if command == "character" || command == "char" || command == "ch" { // char(`char_preset_string`, face_expression = undefined)  optional argument 1 for changing the expression - could be either the name of the expression or the index of it in the sprite
	var __exp = (array_length(arg) > 1 ? real(arg[1]) : 0)
    
    if arg[0] == char && array_length(arg) > 1 { // the user is misusing the char command. refer to face_ex (im crying)
        face_expression = __exp
        if string_length(face_expression) == string_length(string_digits(face_expression))
            face_expression = real(face_expression)
    }
    else {
    	_facechange(arg[0], __exp)
    	
    	voice = struct_get(struct_get(char_presets, arg[0]), "voice")
    	voice_pitch_calc = struct_get(struct_get(char_presets, arg[0]), "voice_pitch_calc")
    	voice_interrupt = struct_get(struct_get(char_presets, arg[0]), "voice_interrupt")
    	voice_skip = struct_get(struct_get(char_presets, arg[0]), "voice_skip")
        voice_pitchrange = struct_get(struct_get(char_presets, arg[0]), "voice_pitchrange")
        
        var __char_font = struct_get(struct_get(char_presets, arg[0]), "font");
        if !is_undefined(__char_font) {
            if is_string(__char_font) 
                font = loc_font(__char_font);
            else if font_exists(__char_font)
                font = __char_font;
        }
    	
    	char = arg[0]
    	looping = false
    }
}
if command == "face" || command == "f" { // face(`face_preset_string`, face_expression)  optional argument 1 for changing the expression - could be either the name of the expression or the index of it in the sprite
	var __exp = (array_length(arg) > 1 ? real(arg[1]) : 0)
	_facechange(arg[0], __exp)
	looping = false
}
if command == "f_ex" || command == "face_ex" || command == "face_expression" { // f_ex(string OR real) OR face_ex(string OR real)
	face_expression = arg[0]
    if string_length(face_expression) == string_length(string_digits(face_expression))
        face_expression = real(face_expression)
}
if command == "voice" || command == "v" { // voice(asset OR nil, pitch_range = undefined, interrupt = undefined, skip_frames = undefined)  write it with the `` things
    voice_pitchrange = undefined
    
	if array_length(arg) > 0 {
		if arg[0] == "nil"
			voice = -1
		else
			voice = asset_get_index(arg[0])
	} 
	if array_length(arg) > 1 && arg[1] != "nil" {
		voice_pitchrange = arg[1]
		voice_pitchrange = string_copy(voice_pitchrange, 1, string_width(voice_pitchrange)-2)
		voice_pitchrange = string_split(voice_pitchrange, ",")
	}
	if array_length(arg) > 2 && arg[2] != "nil" 
		voice_interrupt = string_to_bool(arg[2])
	if array_length(arg) > 3 && arg[3] != "nil" 
		voice_skip = string_to_bool(arg[3])
}

if command == "pitch" || command == "pt" { // pitch([val OR min], [max]) -- setting no arguments resets pitch
	if array_length(arg) > 0 {
		if array_length(arg) == 1
		{
			voice_pitchrange = [arg[0], arg[0]];
		}
		else {
			voice_pitchrange = [arg[0], arg[1]];
		}
	}
	else {
		voice_pitchrange = undefined;
	}
	
}

if command == "mini" || command == "m" { // mini(`text`, char = undefined, face_expression = undefined, x = `auto`, y = `auto`)
    draw_set_font(loc_font("main"))
    
    var __char = undefined
    var __face_ex = 0
    var __xx = x + 500 - string_width(arg[0]) - face_xoff
    var __yy = y + 64
    
    if array_length(arg) > 1 __char = arg[1]
    if array_length(arg) > 2 __face_ex = arg[2]
    if array_length(arg) > 3 && arg[3] != "auto" __xx += real(arg[3])
    if array_length(arg) > 4 && arg[4] != "auto" __yy += real(arg[4])
        
    if string_length(string_digits(__face_ex)) == string_length(__face_ex)
        __face_ex = real(__face_ex)
    
    array_push(mini_faces, 
        instance_create(o_text_mini, __xx, __yy, depth, {
            face_creator: (struct_exists(char_presets, __char) 
                ? struct_get(struct_get(char_presets, __char), "face_create") 
                : undefined
            ),
            face_expression: __face_ex,
            text: arg[0]
        })
    );
}


if command == "money_display" || command == "mon" { // money_display(sell_type)      sell_type can be one of the following: "consumable", "weapon", "armor"
    var sell_type = ITEM_TYPE.CONSUMABLE
    switch arg[0] {
        case "consumable":
            sell_type = ITEM_TYPE.CONSUMABLE
            break
        case "weapon":
            sell_type = ITEM_TYPE.WEAPON
            break
        case "armor":
            sell_type = ITEM_TYPE.ARMOR
            break
    }
    
    instance_create(o_ui_money_display,,,, {sell_type: sell_type})
}
if command == "money_display_hide" || command == "mon_hide" { // money_display_hide
    instance_destroy(o_ui_money_display)
}

argstrings = ""